// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext.kotlin_version = "1.4.21"
    ext.dokka_version = '0.9.17'
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:4.0.0"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        // safe-args
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:2.3.2"
        classpath "org.jetbrains.dokka:dokka-android-gradle-plugin:${dokka_version}"
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

subprojects { // 配下のモジュール全てに適用される
    switch (project.name) {
        case 'app':
            apply plugin: 'com.android.application'
            break
        default:
            apply plugin: 'com.android.library'
            break
    }

    apply plugin: 'kotlin-android'
    apply plugin: 'kotlin-kapt'

    android {
        compileSdkVersion 30
        buildToolsVersion "30.0.3"

        defaultConfig {
            minSdkVersion 28
            targetSdkVersion 30
            versionCode 2
            versionName "1.0.1"

            testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        }

        buildTypes {
            release {
                minifyEnabled false
                proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            }
            debug {
                testCoverageEnabled true
            }
        }

        lintOptions {
            checkReleaseBuilds false
            // Or, if you prefer, you can continue to check for errors in release builds,
            // but continue the build even when errors are found:
            abortOnError false
            xmlReport true
        }
        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }
        kotlinOptions {
            jvmTarget = '1.8'
        }
        buildFeatures {
            dataBinding true
            viewBinding true
        }
    }

    configurations { ktlint }

    afterEvaluate {
        check.dependsOn ktlint
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
        implementation 'androidx.core:core-ktx:1.3.2'
        implementation 'androidx.appcompat:appcompat:1.2.0'
        androidTestImplementation 'androidx.test.ext:junit:1.1.2'
        androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
        implementation fileTree(dir: "libs", include: ["*.jar"])

        // ktlint
        ktlint "com.pinterest:ktlint:0.41.0"

        // koin
        def koin_version = "2.0.1"
        implementation "org.koin:koin-android:$koin_version"
        implementation "org.koin:koin-android-scope:$koin_version"
        implementation "org.koin:koin-android-viewmodel:$koin_version"
        // coroutines
        implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.2'
        implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.1'

        // test
        testImplementation 'junit:junit:4.13.1'
        testImplementation "com.nhaarman:mockito-kotlin:1.5.0"
        // livedata-test
        def livedata_test_version = "2.1.0"
        testImplementation "androidx.arch.core:core-common:$livedata_test_version"
        testImplementation "androidx.arch.core:core-runtime:$livedata_test_version"
        testImplementation "androidx.arch.core:core-testing:$livedata_test_version"
        // coroutine-test
        testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.3.0'
        testImplementation 'io.mockk:mockk:1.10.3-jdk8'
    }
    kotlin {
        sourceSets.all {
            languageSettings.enableLanguageFeature('InlineClasses')
        }
    }

    task ktlint(type: JavaExec, group: "verification") {
        description = "Check Kotlin code style."
        main = "com.pinterest.ktlint.Main"
        classpath = configurations.ktlint
        args "--android", "--color", "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/reports/ktlint-results.xml", "src/**/*.kt"
        ignoreExitValue true
    }

    task ktlintFormat(type: JavaExec, group: "formatting") {
        description = "Fix Kotlin code style deviations."
        main = "com.pinterest.ktlint.Main"
        classpath = configurations.ktlint
        args "-F", "--android", "src/**/*.kt"
        ignoreExitValue true
    }
}


task clean(type: Delete) {
    delete rootProject.buildDir
}