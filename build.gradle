// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext.kotlin_version = "1.5.0"
    ext.dokka_version = '0.9.17'
    ext.hilt_version = '2.35'
    repositories {
        google()
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.2.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlin_version}"
        classpath "com.google.dagger:hilt-android-gradle-plugin:$hilt_version"
        // safe-args
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:2.3.2"
        classpath "org.jetbrains.dokka:dokka-android-gradle-plugin:${dokka_version}"
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        jcenter()
    }
}

subprojects { // 配下のモジュール全てに適用される
    switch (project.name) {
        case 'app':
            apply plugin: 'com.android.application'
            break
        default:
            apply plugin: 'com.android.library'
            break
    }

    apply plugin: 'kotlin-android'
    apply plugin: 'kotlin-kapt'
    apply plugin: 'dagger.hilt.android.plugin'

    android {
        compileSdkVersion 30
        buildToolsVersion "30.0.3"

        defaultConfig {
            minSdkVersion 28
            targetSdkVersion 30
            versionCode 4
            versionName "1.0.3"

            testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        }

        buildTypes {
            release {
                minifyEnabled false
                proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            }
            debug {
                testCoverageEnabled true
            }
        }

        lintOptions {
            checkReleaseBuilds false
            // Or, if you prefer, you can continue to check for errors in release builds,
            // but continue the build even when errors are found:
            abortOnError false
            xmlReport true
        }
        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }
        kotlinOptions {
            jvmTarget = '1.8'
        }
        buildFeatures {
            dataBinding true
            viewBinding true
        }
    }

    configurations { ktlint }

    afterEvaluate {
        check.dependsOn ktlint
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
        implementation 'androidx.core:core-ktx:1.3.2'
        implementation 'androidx.appcompat:appcompat:1.2.0'
        androidTestImplementation 'androidx.test.ext:junit:1.1.2'
        androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
        implementation fileTree(dir: "libs", include: ["*.jar"])

        // ktlint
        ktlint "com.pinterest:ktlint:0.41.0"

        // hilt
        implementation "com.google.dagger:hilt-android:$rootProject.hilt_version"
        kapt "com.google.dagger:hilt-android-compiler:$rootProject.hilt_version"
        // coroutines
        implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.2'
        implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.1'
        // timber
        def timber_version = "4.7.1"
        implementation "com.jakewharton.timber:timber:$timber_version"

        // test
        testImplementation 'junit:junit:4.13.1'
        testImplementation "com.nhaarman:mockito-kotlin:1.5.0"
        // livedata-test
        def livedata_test_version = "2.1.0"
        testImplementation "androidx.arch.core:core-common:$livedata_test_version"
        testImplementation "androidx.arch.core:core-runtime:$livedata_test_version"
        testImplementation "androidx.arch.core:core-testing:$livedata_test_version"
        // coroutine-test
        testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.3.0'
        testImplementation 'io.mockk:mockk:1.10.3-jdk8'
    }
    kotlin {
        sourceSets.all {
            languageSettings.enableLanguageFeature('InlineClasses')
        }
    }

    task ktlint(type: JavaExec, group: "verification") {
        description = "Check Kotlin code style."
        main = "com.pinterest.ktlint.Main"
        classpath = configurations.ktlint
        args "--android", "--color", "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/reports/ktlint-results.xml", "src/**/*.kt"
        ignoreExitValue true
    }

    task ktlintFormat(type: JavaExec, group: "formatting") {
        description = "Fix Kotlin code style deviations."
        main = "com.pinterest.ktlint.Main"
        classpath = configurations.ktlint
        args "-F", "--android", "src/**/*.kt"
        ignoreExitValue true
    }
}

apply plugin: "jacoco"

jacoco {
    toolVersion = "0.8.7"
}

// 全プロジェクトのカバレッジ取得
task jacocoMergeReports(type: JacocoMerge) {
    group = 'verification'
    description = "Merge all JaCoCo reports from projects into one."

    gradle.afterProject { p, state ->
        if (p.rootProject != p && p.plugins.hasPlugin('jacoco')) {
            if (p.name != "document" && p.name != "ChatMemo") {
                dependsOn "${p.path}:createDebugCoverageReport"
                dependsOn "${p.path}:testDebugUnitTest"
                executionData "${p.buildDir}/jacoco/testDebugUnitTest.exec"
            }
        }
    }
}

// マージされたカバレッジ取得
task jacocoTestReports(type: JacocoReport, dependsOn: 'jacocoMergeReports') {
    group = 'verification'
    description = "Generate Jacoco coverage reports for the build. Only unit tests."

    executionData jacocoMergeReports.destinationFile

    getSourceDirectories().setFrom(files())
    getClassDirectories().setFrom(files())

    gradle.afterProject { p, state ->
        if (p.rootProject != p && p.plugins.hasPlugin('jacoco')) {
            if (p.name != "document" && p.name != "ChatMemo") {
                getSourceDirectories().from += "${p.projectDir}/src/main/java"
                getClassDirectories().from += p.fileTree(dir: "${p.buildDir}/tmp/kotlin-classes/debug")
                getExecutionData().setFrom(fileTree(dir: project.projectDir, includes: [
                        '**/*.exec',    //JUnit Test Result
                        '**/*.ec'])     //Espresso Test Result
                )
            }
        }
    }

    reports {
        xml.enabled true
        html.enabled true
        csv.enabled false
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}